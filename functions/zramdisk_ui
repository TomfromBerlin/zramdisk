##########################################################################
#                                                                        #
#     Everything below this line comes with no warranty of any kind.     #
#                 Use these file at your own risk!                       #
#                                                                        #
# project start: 2025-10 | first release: 2026-02 | last update: 2026-02 #
##########################################################################
#                                                                        #
# The MIT license applies to this and all related files.                 #
# If you want to modify, remix the code, or give it to your cat, this is #
# only possible under the terms of this license, and you must distribute #
# a copy of this license along with the remixed or modified code.        #
# Yes, even to your cat.                                                 #
#                                                                        #
# TomfromBerlin 2025-2026                                                #
##########################################################################

zramdisk_ui_init() {
    emulate -L zsh
    if [[ -f "${ZRAMDISK_PLUGIN_DIR}"/no_color ]] ; then
        ZRAMDISK_COLOR_GREEN_BOLD=
        ZRAMDISK_COLOR_GREEN=$'\e[0m'
        ZRAMDISK_COLOR_RED_BOLD=
        ZRAMDISK_COLOR_RED=$'\e[3;37;40m'
        ZRAMDISK_COLOR_YELLOW=$'\e[3;37;40m'
        ZRAMDISK_COLOR_BLUE_BOLD=
        ZRAMDISK_COLOR_BLUE=$'\e[3;37;40m'
        ZRAMDISK_COLOR_CYAN_BOLD=
        ZRAMDISK_COLOR_CYAN=$'\e[0m'
        ZRAMDISK_COLOR_MAGENTA_BOLD=
        ZRAMDISK_COLOR_MAGENTA=$'\e[0m'
        ZRAMDISK_COLOR_WHITE=
        ZRAMDISK_COLOR_WHITE_FLASH=$'\e[5;37;40m'
        ZRAMDISK_COLOR_GREY=$'\e[2;37m'
        ZRAMDISK_COLOR_NC=$'\e[0m'
        ZRAMDISK_COLOR_ITALIC=$'\e[3m'
    else
        ZRAMDISK_COLOR_GREEN_BOLD=$'\e[0;32;1m'
        ZRAMDISK_COLOR_GREEN=$'\e[0;32m'
        ZRAMDISK_COLOR_RED_BOLD=$'\e[0;31;1m'
        ZRAMDISK_COLOR_RED=$'\e[0;31m'
        ZRAMDISK_COLOR_YELLOW=$'\e[1;33m'
        ZRAMDISK_COLOR_BLUE_BOLD=$'\e[1;34m'
        ZRAMDISK_COLOR_BLUE=$'\e[0;34m'
        ZRAMDISK_COLOR_CYAN_BOLD=$'\e[1;36m'
        ZRAMDISK_COLOR_CYAN=$'\e[0;36m'
        ZRAMDISK_COLOR_MAGENTA_BOLD=$'\e[0;35;1m'
        ZRAMDISK_COLOR_MAGENTA=$'\e[0;35m'
        ZRAMDISK_COLOR_WHITE=$'\e[0;37;40m'
        ZRAMDISK_COLOR_WHITE_FLASH=$'\e[5;37;40m'
        ZRAMDISK_COLOR_GREY=$'\e[2;37m'
        ZRAMDISK_COLOR_NC=$'\e[0m'
        ZRAMDISK_COLOR_ITALIC=$'\e[3m'
    fi
}

# Quick & Dirty: replace familiar 2-column symbols to make the simple length more plausible.
zramdisk_normalize_wide_placeholder() {
#zramdisk_debug "${ZRAMDISK_COLOR_CYAN}zramdisk_ui:${ZRAMDISK_COLOR_NC} Reached function zramdisk_normalize_wide_placeholder" >&2
    emulate -L zsh
    setopt LOCALOPTIONS
# e_w = emoji_widths mapping (emoji ‚Üí display width in 'O' chars)
#local distrib_id
#distrib_id="$(awk -F= '/^DISTRIB_ID/ {print $2}' /etc/lsb-release)"
local distro
distro=$(lsb_release -si)
case "${distro}" in
*mint|LinuxMint|*Ubuntu|\"void\"|VoidLinux|voidlinux)
local -A e_w=(
    üü¢ OO  üî¥ OO  üü° OO  üîµ OO
    ‚ö™ OO  ‚úÖ OO  üí≤ OO  ‚úîÔ∏è OO
    ‚ö†Ô∏è OO  ‚ùå OO  üéâ OO  ‚ö° OO
    ‚ö´ OO
    ‚ùî O  ü´ü O  ‚ÄºÔ∏è O  ‚ÑπÔ∏è O  ‚öôÔ∏è O
)
;;
cachyos|Debian|Fedora)
local -A e_w=(
    üü¢ OO  üî¥ OO  üü° OO  üîµ OO
    ‚öôÔ∏è OO  ‚úÖ OO  üí≤ OO  ‚úîÔ∏è OO
    ‚ö†Ô∏è OO  ‚ùå OO  üéâ OO  ‚ö° OO
    ‚ö´ OO  ‚ö™ OO  ‚ÑπÔ∏è OO
    ‚ùî O  ü´ü O  ‚ÄºÔ∏è O
)
;;
esac

local em wi s="$1"
for em wi in ${(kv)e_w}; do
    s=${s//$em/$wi}
done
printf "%s" "$s"
}

zramdisk_calc_visible_length() {
    emulate -L zsh
    setopt LOCALOPTIONS

    local str="$1"

    # Remove ANSI escape sequences; we use sed for this
    local cleanup
    cleanup=$(echo -E "$str" | sed -r 's/\x1b\[[0-9;]*m//g')

    # Replace emojis with 2-space-placeholder
    cleanup=$(zramdisk_normalize_wide_placeholder "$cleanup")

    # Simulate prompt expansion for %%
    cleanup=$(echo -E "$cleanup" | sed 's/%%/%/g')

    echo ${#cleanup}
}

zramdisk_print_box() {
# Usage (the following commented lines must be placed and uncommented somewhere in a script or function,
# where the output should be generated. You must not uncomment it here, or your output will be scrambled:
# local -a title body footer msg
# title=("title string, will be centered")
# body=("
# body content, multiple rows possible.
# Just define the vars you need and let the others go.
# Then choose the vars after 'zramdisk_print_box' and delete the others.
# You can also ${ZRAMDISK_COLOR_GREEN}colorize${ZRAMDISK_COLOR_NC} the output.
# ")
# footer=("string, will be centered, also multiple lines are possible")
# msg=("msg string, each row will be prefixed with an icon, depending on the trigger word
# [ask] [info] [ok] [err] [warn] [conf]")
# zramdisk_print_box \
#   "${title[@]}" \
#   "${body[@]}" \
#   "${footer[@]}" \
#   "info" \
#   "${msg[@]}"
    emulate -L zsh
    setopt LOCALOPTIONS no_aliases
    clear

    # ----------------------------------------------------------------------
    # PARAMETER
    # ----------------------------------------------------------------------

    local title="$1" ; (( $# )) && shift

    local body_raw="$1" ; (( $# )) && shift
    local -a body_lines=("${(@f)body_raw}")

    local footer_raw="$1" ; (( $# )) && shift
    local -a footer_lines=("${(@f)footer_raw}")

    local msg_type="$1" ; (( $# )) && shift

    local msg_raw="$1" ; (( $# )) && shift
    local -a msg_lines=("${(@f)msg_raw}")

    # ----------------------------------------------------------------------
    # CHECK WHICH SECTIONS HAVE CONTENT
    # ----------------------------------------------------------------------
    local has_body=0
    local has_footer=0
    local has_msg=0

    # Check if *_raw contains content
    [[ -n "${body_raw}" ]] && has_body=1
    [[ -n "${footer_raw}" ]] && has_footer=1
    # Check if msg_raw contains content AND msg_type is a valid type
    [[ -n "${msg_raw}" && "${msg_type}" =~ ^(ask|info|ok|conf|warn|err)$ ]] && has_msg=1

    # ----------------------------------------------------------------------
    # CALCULATE DYNAMIC WIDTH
    # ----------------------------------------------------------------------

    if command -v tput >/dev/null && cols=$(tput cols) && (( cols > 120 )); then
        max_width=$(( cols * 3 / 4 ))
    else
        max_width=90
    fi
    local min_width=30    # <- lower limit (optional)
    local actual_width=0

    local x len

    # Check title
    len=$(zramdisk_calc_visible_length "$title")
    (( len > actual_width )) && actual_width=$len

    # Check body lines
    for x in "${body_lines[@]}"; do
        len=$(zramdisk_calc_visible_length "$x")
        (( len > actual_width )) && actual_width=$len
    done

    # Check footer lines
    for x in "${footer_lines[@]}"; do
        len=$(zramdisk_calc_visible_length "$x")
        (( len > actual_width )) && actual_width=$len
    done

    # Check message lines
    for x in "${msg_lines[@]}"; do
        len=$(zramdisk_calc_visible_length "$x")
        # message also includes symbol ‚Üí minimal +2
        (( len + 2 > actual_width )) && actual_width=$(( len + 2 ))
    done

    # Padding: right + left
    inner_width=$(( actual_width - 2 ))
    (( actual_width += 2 ))

    # Applies min/max limits
    (( actual_width < min_width )) && actual_width=$min_width
    (( actual_width > max_width )) && actual_width=$max_width

    local width=$actual_width
    local line inner_line
    line=$(printf '‚îÄ%.0s' {1..$width})
    inner_line=$(printf '‚îÄ%.0s' {1..$inner_width})

    # ----------------------------------------------------------------------
    # HEADER
    # ----------------------------------------------------------------------
    print -P "${ZRAMDISK_COLOR_MAGENTA}‚ï≠${line}‚ïÆ${ZRAMDISK_COLOR_NC}"

    # ----------------------------------------------------------------------
    # TITLE (always centered)
    # ----------------------------------------------------------------------
    local tlen=$(zramdisk_calc_visible_length "$title")
    local tpad=$(( (width - tlen) / 2 ))
    local tpad_r=$(( width - tlen - tpad ))
# The following lines must not be indented, otherwise the padding will not be calculated correctly.
print -P "${ZRAMDISK_COLOR_MAGENTA}‚îÇ${ZRAMDISK_COLOR_NC}\
$(printf ' %.0s' {1..$tpad})${title}\
$(printf ' %.0s' {1..$tpad_r})\
${ZRAMDISK_COLOR_MAGENTA}‚îÇ${ZRAMDISK_COLOR_NC}"

    if (( has_body || has_footer || has_msg )) ; then
        print -P "${ZRAMDISK_COLOR_MAGENTA}‚îú${line}‚î§${ZRAMDISK_COLOR_NC}"
    fi

    # ----------------------------------------------------------------------
    # BODY
    # ----------------------------------------------------------------------
    if (( has_body )) ; then
        for line_text in "${body_lines[@]}"; do
            vlen=$(zramdisk_calc_visible_length "$line_text")
            pad=$(( width - vlen ))
            printf -v padding "%${pad}s" ""
            print -P "${ZRAMDISK_COLOR_MAGENTA}‚îÇ${ZRAMDISK_COLOR_NC}${line_text}${padding}${ZRAMDISK_COLOR_MAGENTA}‚îÇ${ZRAMDISK_COLOR_NC}"
        done
    fi

    # ----------------------------------------------------------------------
    # FOOTER (always centered)
    # ----------------------------------------------------------------------
    if (( has_footer )) ; then
        if (( has_body || has_msg )) ; then
            print -P "${ZRAMDISK_COLOR_MAGENTA}‚îú${line}‚î§${ZRAMDISK_COLOR_NC}"
        fi

        for x in "${footer_lines[@]}"; do
            local flen=$(zramdisk_calc_visible_length "$x")
            local fpad=$(( (width - flen) / 2 ))
            local fpad_r=$(( width - flen - fpad ))
# The following lines must not be indented, otherwise the padding will not be calculated correctly.
print -P "${ZRAMDISK_COLOR_MAGENTA}‚îÇ${ZRAMDISK_COLOR_NC}\
$(printf ' %.0s' {1..$fpad})${x}\
$(printf ' %.0s' {1..$fpad_r})\
${ZRAMDISK_COLOR_MAGENTA}‚îÇ${ZRAMDISK_COLOR_NC}"
        done
    fi

    # ----------------------------------------------------------------------
    # MESSAGES
    # ----------------------------------------------------------------------
    if (( has_msg )); then
        if (( has_body || has_footer )) ; then
            print -P "${ZRAMDISK_COLOR_MAGENTA}‚îú${line}‚î§${ZRAMDISK_COLOR_NC}"
        fi

        local color symbol msg mpad mlen msgpad
        # Icons are hard-coded to avoid font dependencies.
        # If you add or replace other icons (and trigger words),
        # be sure to include them in the zramdisk_normalize_wide_placeholder function,
        # otherwise the padding will not work correctly.
        case "$msg_type" in
            ask)  symbol="‚ùî " ;;
            info) symbol="‚ÑπÔ∏è " ;;
            ok)   symbol="‚úÖ " ;;
            conf) symbol="‚öôÔ∏è " ;;
            warn) symbol="‚ö†Ô∏è " ;;
            err)  symbol="‚ùå " ;;
              *)  symbol="ü´ü " ;;
        esac

        for msg in "${msg_lines[@]}"; do
            if [[ $msg_type == "ask" || $msg_type == "conf" ]] ; then
                mlen=$(zramdisk_calc_visible_length "$symbol$msg")
                mpad=$(( width - mlen - 1 ))
            else
                mlen=$(zramdisk_calc_visible_length "$symbol$msg")
                mpad=$(( width - mlen ))
            fi
            printf -v msgpad "%${mpad}s"
            print -P "${ZRAMDISK_COLOR_MAGENTA}‚îÇ${symbol}${msg}${msgpad}${ZRAMDISK_COLOR_MAGENTA}‚îÇ${ZRAMDISK_COLOR_NC}"
        done
    fi

    # ----------------------------------------------------------------------
    # BOTTOM LINE
    # ----------------------------------------------------------------------
    print -P "${ZRAMDISK_COLOR_MAGENTA}‚ï∞${line}‚ïØ${ZRAMDISK_COLOR_NC}"
}
# Initialize color variables immediately upon loading
zramdisk_ui_init

#ZRAMDISK_SCRIPT
